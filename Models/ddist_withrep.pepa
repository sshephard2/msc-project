/*
 * Replicated distributed DB model
 *
 * Without middleware
 *
 * DB node 1 for Athletics and Cycling tickets
 * DB node 2 for Cycling and Diving tickets
 * DB node 3 for Diving and Athletics tickets
 *
 */

a = 2.0; /* Rate of booking Athletics tickets */
c = 1.0; /* Rate of booking Cycling tickets */
d = 1.0; /* Rate of booking Diving tickets */

db = 2.0; /* Rate of servicing DB requests */

/* Web front-end process */
Website = (book_athletics, a).Website + (book_cycling, c).Website  + (book_diving, d).Website;

/* DB node processes */
DBnode_1 = (book_athletics, T).DBnode_1_srv + (book_cycling, T).DBnode_1_srv;
DBnode_1_srv = (srv_db_1, T).DBnode_1;

DBnode_2 = (book_cycling, T).DBnode_2_srv + (book_diving, T).DBnode_2_srv;
DBnode_2_srv = (srv_db_2, T).DBnode_2;

DBnode_3 = (book_diving, T).DBnode_3_srv + (book_athletics, T).DBnode_3_srv;
DBnode_3_srv = (srv_db_3, T).DBnode_3;

/* Service processes for DB nodes */
Service1 = (srv_db_1, db).Service1;
Service2 = (srv_db_2, db).Service2;
Service3 = (srv_db_3, db).Service3;

Website <book_athletics, book_cycling> DBnode_1
  <book_cycling, book_diving> DBnode_2
  <book_diving, book_athletics> DBnode_3
  <srv_db_1> Service1
  <srv_db_2> Service2
  <srv_db_3> Service3
