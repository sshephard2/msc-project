/*
 * Replicated distributed DB model
 *
 * Without middleware
 *
 * DB node 1 for Athletics and Cycling tickets
 * DB node 2 for Cycling and Diving tickets
 * DB node 3 for Diving and Athletics tickets
 *
 */

a = 1.0; /* Rate of booking Athletics tickets */
c = 1.0; /* Rate of booking Cycling tickets */
d = 1.0; /* Rate of booking Diving tickets */

db = 5.0; /* Rate of servicing DB requests */

/* Web front-end process */
Website = (athletics, a).Website + (cycling, c).Website  + (diving, d).Website;

/* DB node processes */
DB_1 = (athletics, T).DBsrv_1 + (cycling, T).DBsrv_1;
DBsrv_1 = (dbsrv1, T).DB_1;

DB_2 = (cycling, T).DBsrv_2 + (diving, T).DBsrv_2;
DBsrv_2 = (dbsrv2, T).DB_2;

DB_3 = (diving, T).DBsrv_3 + (athletics, T).DBsrv_3;
DBsrv_3 = (dbsrv3, T).DB_3;

/* Service processes for DB nodes */
Service_1 = (dbsrv1, db).Service_1;
Service_2 = (dbsrv2, db).Service_2;
Service_3 = (dbsrv3, db).Service_3;

Website <athletics, cycling, diving> (DB_1 || DB_2 || DB_3)
	<dbsrv1, dbsrv2, dbsrv3> (Service_1 || Service_2 || Service_3)