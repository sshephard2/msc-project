/*
 * Simple Microservices model
 *
 * one DB for Athletics tickets, one separate DB for Cycling
 *
 */

a = 1.0; /* Rate of booking Athletics tickets */
c = 1.0; /* Rate of booking Cycling tickets */

w = 100.0; /* Rate of consuming from worker applications */
db = 6.5; /* Rate of servicing DB requests */

/* Web front-end process */
Website = (athletics, a).Website + (cycling, c).Website;

/* Worker processes */
Worker_A = (athletics, T).WorkerSrv_A;
WorkerSrv_A = (workerA, T).Worker_A;

Worker_C = (cycling, T).WorkerSrv_C;
WorkerSrv_C = (workerC, T).Worker_C;

/* DB  processes */
DB_1 = (workerA, w).DBsrv_1;
DBsrv_1 = (dbsrv1, db).DB_1;

DB_2 = (workerC, w).DBsrv_2;
DBsrv_2 = (dbsrv2, db).DB_2;

/* Service processes for DBs */
Service_1 = (dbsrv1, db).Service_1;
Service_2 = (dbsrv2, db).Service_2;

Service_1 <dbsrv1> DB_1 <workerA> Worker_A <athletics> Website
  <cycling> Worker_C <workerC> DB_2 <dbsrv2> Service_2