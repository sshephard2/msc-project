/*
 * Replicated distributed DB model
 *
 * middleware architecture with distributed database with replication
 * DB node 1 for Athletics and Cycling tickets
 * DB node 2 for Cycling and Diving tickets
 * DB node 3 for Diving and Athletics tickets
 *
 */

a = 1.0; /* Rate of booking Athletics tickets */
c = 1.0; /* Rate of booking Cycling tickets */
d = 1.0; /* Rate of booking Diving tickets */

q = 100.0; /* Rate of consuming from queue */
db = 5.0; /* Rate of servicing DB requests */

/* Web front-end process */
Website = (athletics, a).Website + (cycling, c).Website  + (diving, d).Website;

/* Shared queue process */
Q_0 = (athletics, T).Q_A + (cycling, T).Q_C + (diving, T).Q_D;
Q_A = (queueA, T).Q_0;
Q_C = (queueC, T).Q_0;
Q_D = (queueD, T).Q_0;

/* DB node processes */
DB_1 = (queueA, q).DBsrv_1 + (queueC, q).DBsrv_1;
DBsrv_1 = (dbsrv1, T).DB_1;

DB_2 = (queueC, q).DBsrv_2 + (queueD, q).DBsrv_2;
DBsrv_2 = (dbsrv2, T).DB_2;

DB_3 = (queueD, q).DBsrv_3 + (queueA, q).DBsrv_3;
DBsrv_3 = (dbsrv3, T).DB_3;

/* Service processes for DB nodes */
Service_1 = (dbsrv1, db).Service_1;
Service_2 = (dbsrv2, db).Service_2;
Service_3 = (dbsrv3, db).Service_3;

Website <athletics, cycling, diving> Q_0[10]
 	<queueA, queueC, queueD> (DB_1 || DB_2 || DB_3)
	<dbsrv1, dbsrv2, dbsrv3> (Service_1 || Service_2 || Service_3)
